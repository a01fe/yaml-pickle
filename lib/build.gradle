plugins {
    id 'scala'
    id 'java-library'
    id 'maven-publish'
    // id 'eu.kakde.gradle.sonatype-maven-central-publisher' version '1.0.6'
}

group = 'io.github.a01fe'
version = parent.version

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.scala-lang:scala3-library_3:3.7.0'
    implementation 'org.snakeyaml:snakeyaml-engine:2.9'
    implementation 'com.lihaoyi:upickle_3:4.2.1'
    // os-lib leaks to api because our common_profile upickle profile has an implicit
    // that references os-lib to serialize os.Relpath
    api 'com.lihaoyi:os-lib_3:0.11.4'

    testImplementation 'org.scalameta:munit_3:1.1.1'
    testImplementation 'org.scalameta:munit-scalacheck_3:1.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.2'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.additionalParameters += [
        '-Wunused:all'  // cspell:disable-line
    ]
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'yaml-pickle'
            from components.java
        }
    }
    repositories {
        maven {
            url = version.endsWith("SNAPSHOT") ? "${easNexusUrl}/maven-snapshots" : "${easNexusUrl}/maven-releases"
            credentials {
                username = easNexusUsername
                password = easNexusPassword
            }
        }
    }
}

tasks.named('test', Test) {
    useJUnitPlatform()
}
